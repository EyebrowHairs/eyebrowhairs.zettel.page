---
id: 202005250938
title: AutoHotkey Scripts
date: 2020-05-25
tags: [project, tech]
---

:::{.ui .message}
See the latest version of the scripts here: [https://github.com/EyebrowHairs/AHK-Scripts](https://github.com/EyebrowHairs/AHK-Scripts)
:::

## Inspiration

Due to the lack of a few features I desired on [Zettlr](https://www.zettlr.com/), a Markdown note-taking app, I decided to explore AutoHotkey to see if it could be of any use. After two whole days and **much** Googling to figure it out, I basically stitched together another 'Frankenstein' script for keyboard shortcuts.

## Sources

*(that I basically copied from, so many thanks to their creators!!!!!)*

1. Made a few tweaks and additions to this [script](https://forum.zettlr.com/discussion/122/creating-linked-zettel-with-autohotkey) to create and link new Zettel (thanks to user 6bqh22d2he!). Check out the post to see their script in action.
2.  Modified and was inspired by this [script](https://www.labnol.org/software/tutorials/simple-diary-tutorial-with-autohotkey/4902/) for new note creation.
3. Inspired by this [post](https://www.reddit.com/r/Zettlr/comments/gn96wr/templates_for_new_notes_zettel/?utm_source=share&utm_medium=web2x) for the YAML template.
4. Multi-line input code from [here](https://stackoverflow.com/questions/45986293/how-can-i-read-multiple-lines-of-user-input-in-autohotkey).

## Uses

1. Create a new 'daily' type note from anywhere (*ctrl + alt + n, type note, press ok*),
2. Create and link a child Zettel within an existing note (*highlight text, then press ctrl + alt + z*), and
3. Insert a YAML template complete with a new ID (*ctrl + alt + y*).

## How to use (for newbies like me)

*Unfortunately AHK is only supported on* ***Windows***. *I did come across a cross-platform text-expander called* [*espanso*](https://espanso.org/)*, but haven't explored that.*

1. Follow steps A and B of this [tutorial](https://www.autohotkey.com/docs/Tutorial.htm)
2. Copy the script text in place of the example script.
3. Insert the folder paths of where you want new files to be created (there are 3 spots).
4. Run the file and try it out!

## Script

Iâ€™m still tweaking things around, so the latest version is on Github: [https://github.com/EyebrowHairs/AHK-Scripts](https://github.com/EyebrowHairs/AHK-Scripts)

; = comment

    ;AutoHotKey Zettlr Shortcuts TEMPLATE. You will need to change the folder locations for the first two scripts (marked with *)
    
    ;Create Quick Note that creates a new dated note from anywhere on your computer. New note will have auto-filled Numerical ID and dated title in YAML header, and time stamp as heading for new entries.
    
      ;Shortcut (CTRL + ALT + N)
        ^!N::
        
      ;Show the Input Box to the user
        ;Gui, Color, 1CB27E ;Optional green background color, remove the ';' in front of 'Gui'
        Gui, Add, Text,, Text (can format in MD): ;Change the last text for a different message
        Gui, Add, Edit, w400 h100 vtext ;Change the numbers for a different textbox size
        Gui, Add, Button, gokay_pressed, Okay
        Gui, Add, Button, cancel X+8 YP+0, Cancel
        Gui, Show, Center autosize, Add Quick Note to Zettlr
      Return
    
        GuiClose:
        GuiEscape:
        ButtonCancel:
        Gui, Destroy
      return
    
        okay_pressed:
        Gui Submit
        Gui Destroy
        
      ;Create ID
        FormatTime, CurrentDateTime,, yyyyMMddHHmm ;(add 'ss' to the end if using seconds)
        id=%CurrentDateTime%
        
      ;Create date stamp
        FormatTime, CurrentDate,, MMMM d, yyyy
        dateStamp=%CurrentDate%
        
      ;Format the time stamp
        timeStamp=%A_Hour%:%A_Min%
        
      ;Define folder. CHANGE THIS TO YOUR ZETTLR WORKING FOLDER WHERE YOU WANT NEW FILE TO BE CREATED!
        zettlrFolder=C:\Users\---\Dropbox\Personal
        
        ;Write text to a new Zettlr file if not existing, or append to existing one. ADD IN YOUR FOLDER LOCATION PATH AT THE ASTERISK (same as above).
        if FileExist("C:\*\" dateStamp ".md") {
            FileAppend, 
            (
    # %timeStamp%
    %text%`n`n
            ), %zettlrFolder%\%dateStamp%.md
        } else {
            FileAppend, 
            (
    ---
    id: %id%
    title: %dateStamp%
    ---
    # %timeStamp%
    %text%`n`n
            ), %zettlrFolder%\%dateStamp%.md
        }
    return
    
    ;The following scripts only work when Zettlr is open
    
    #IfWinActive ahk_exe Zettlr.exe
    
    ;Create Linked Child Zettel. Highlight the text you want the new note to be titled as, then press the shortcut keys. New note will be automatically given a filename with Numerical ID, and auto-fill ID and Title in YAML header.
    
      ;Shortcut (CTRL + ALT + Z)
        ^!Z::
        KeyWait, Ctrl
        KeyWait, Shift
        
      ;Copy current text selection to clipboard
        Send {Ctrl down}{c}{Ctrl up}
        
      ;Clipboard to variable
        titleText=%clipboard%
        
      ;Create date time string
        FormatTime, CurrentDateTime,, yyyyMMddHHmm ;(add 'ss' to the end if using seconds)
        id=%CurrentDateTime%
        
      ;Paste date time string + titletext to current text cursor position
        clipboard= [[%id%]] %titleText%
        Send {Ctrl down}{v}{Ctrl up}
        
      ;Copy current file name to clipboard using Zettlr GUI and shortcut
        Send {Ctrl down}{r}{Ctrl up}
        Send {Ctrl down}{c}{Ctrl up}
        Send {Esc}
        
      ;Clipboard to variable
        currentFileName= %clipboard%
        
      ;Take prefix .md away from currentFileName string
        StringReplace,currentFileName,currentFileName,.md,, All
        
      ;Define folder. CHANGE THIS TO YOUR ZETTLR WORKING FOLDER WHERE YOU WANT NEW FILE TO BE CREATED!
        zettlrFolder=C:\*
        
      ;Write to new file. First line is link to currentFileName. After that, two newlines.
        FileAppend, ---`nid: %id%`ntitle: %titleText%`n---`nLink: [[%currentFileName%]]`n`n, %zettlrFolder%\%id%.md
        
    return
    
    ;Auto YAML Formatter. Automatically generates ID.
    
      ;Shortcut (CTRL + ALT + Y)
        ^!Y::
        
      ;Create date time string 
        FormatTime, CurrentDateTime,, yyyyMMddHHmm ;(add 'ss' to the end if using seconds)
        id=%CurrentDateTime%
    
      ;YAML Layout (modify as needed)  
        Send, 
        (
    ---
    id: %id%
    title:
    ---
        )
    return

## Notes

* I'm not a programmer so there might be a better solution to these...
* I couldn't figure out how to get Zettlr to open after creating a new note, so you have to open it manually after creating a new note. So the quick note function could be useful if you just wanted to jot a note down quickly and revisit it later. If you make multiple notes in a day, it will add to the same file and add a timestamp to each note.
* You can change the shortcut keys, but make sure it doesn't conflict with any existing ones on your computer or on Zettlr.
* Make sure to reload the script if you make any changes.
* I haven't figured out how to automatically start the scripts yet, so make sure it's running before you try to use it (you should see it amongst the icons on the toolbar).